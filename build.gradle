buildscript {


    repositories {
        mavenLocal()
        mavenCentral()
        gradlePluginPortal()
        maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
        google()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:7.4.2'
        classpath 'com.google.gms:google-services:4.3.15'
    }
}

allprojects {
    apply plugin: "eclipse"
    apply plugin: "checkstyle"

    version = '1.0'
    ext {
        appName = "Sumo Battle Tap"
        gdxVersion = '1.11.0'
        roboVMVersion = '2.3.16'
        box2DLightsVersion = '1.5'
        ashleyVersion = '1.7.4'
        aiVersion = '1.8.2'
        gdxControllersVersion = '2.2.1'
    }

    repositories {
        mavenLocal()
        mavenCentral()
        google()
        gradlePluginPortal()
        maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
        maven { url "https://oss.sonatype.org/content/repositories/releases/" }
        maven { url "https://jitpack.io" }
    }
    configurations {
        checkstyleConfig
    }
    def versions = [
            checkstyle: '10.9.2',
    ]
    checkstyle {
        configFile = rootProject.file('config/checkstyle/sun_checks.xml')
        toolVersion = "${versions.checkstyle}"
        ignoreFailures = false
        maxWarnings = 0
    }
}

project(":desktop") {
    apply plugin: "java-library"


    dependencies {
        implementation project(":core")
        api "com.badlogicgames.gdx:gdx-backend-lwjgl3:$gdxVersion"
        api "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-desktop"

    }
}

project(":android") {
    apply plugin: "com.android.application"
    apply plugin: 'com.google.gms.google-services'
    apply plugin: "checkstyle"

    configurations {
        natives
        checkstyleConfig
    }
    def versions = [
            checkstyle: '10.9.2',
    ]
    dependencies {
        implementation project(":core")
        api "com.badlogicgames.gdx:gdx-backend-android:$gdxVersion"
        natives "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-armeabi-v7a"
        natives "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-arm64-v8a"
        natives "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-x86"
        natives "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-x86_64"
        implementation platform('com.google.firebase:firebase-bom:31.2.3')
        implementation 'com.google.firebase:firebase-database'
    }

    checkstyle {
        configFile = rootProject.file('config/checkstyle/sun_checks.xml')
        toolVersion = "${versions.checkstyle}"
        ignoreFailures = false
        maxWarnings = 0
    }

    android.applicationVariants.all { variant ->
        variant.outputs.each { output ->
            def checkstyleTask = tasks.create(name: "checkstyle${variant.name.capitalize()}",
                    type: Checkstyle, group: 'Verification') {
                configFile = rootProject.file('config/checkstyle/sun_checks.xml')
                source = variant.sourceSets.java.srcDirs.collect { it.path }.flatten()
                include '**/*.java'
                exclude '**/gen/**'
                classpath = files()
            }
            tasks.named("check").configure { dependsOn checkstyleTask }
        }
    }
}


project(":core") {
    apply plugin: "java-library"


    dependencies {
        api "com.badlogicgames.gdx:gdx:$gdxVersion"
    }
}
